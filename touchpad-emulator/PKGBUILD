# Maintainer: Jeremy Kescher <jeremy@kescher.at>

pkgname=touchpad-emulator
pkgver=r21.945054c
pkgrel=1
pkgdesc="Emulate a touchpad mouse device using a touchscreen"
arch=('aarch64')
url="https://gitlab.com/CalcProgrammer1/TouchpadEmulator"
license=('GPL')
source=(${pkgname}::git+https://gitlab.com/CalcProgrammer1/TouchpadEmulator.git
		launch-script)
sha512sums=('SKIP'
            '55766777832ea77f3eb7343b7fec9a7b088728add3791cca11097564d00f74778dea8342512726094e3875cce7cd81fe495d8a4a4754374ee643adbde5a70183')

pkgver() {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "${srcdir}/${pkgname}"
	gcc $CFLAGS TouchpadEmulator.c -o TouchpadEmulator
}

package() {
    cd "${srcdir}/${pkgname}"
	install -Dm755 TouchpadEmulator "${pkgdir}/usr/lib/${pkgname}/TouchpadEmulator"
	install -Dm755 launch-script "${pkgdir}/usr/bin/touchpad-emulator"
}
