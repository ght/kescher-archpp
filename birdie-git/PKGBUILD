pkgname=birdie-git
provides=('birdie')
pkgver=r61.14831c3
pkgrel=1
pkgdesc="Alarm app designed for Linux phones."
arch=('x86_64' 'i686' 'aarch64')
url="https://github.com/Dejvino/birdie"
license=('GPL2')
depends=('python-psutil' 'python-mpris_server' 'phosh')
makedepends=('git')
conflicts=('wake-mobile')
source=("$pkgname::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "$pkgname"
    (
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    sed -i 's%DESTDIR}/lib%DESTDIR}/usr/lib%g' "${pkgname}/Makefile"
    sed -i '/gtk-update-icon-cache/d' "${pkgname}/Makefile"
}

build() {
    cd "$pkgname"
    make
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
}

post_install() {
    gtk-update-icon-cache
}

post_upgrade() {
    gtk-update-icon-cache
}
